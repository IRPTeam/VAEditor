Перем VanessaTabs;

&НаКлиенте
Функция ПрочитатьСтрокуJSON(ТекстJSON, ПрочитатьВСоответствие = Ложь)
	
	Если ПустаяСтрока(ТекстJSON) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПоляДаты = Новый Массив;
	ПоляДаты.Добавить("CreationDate");
	ПоляДаты.Добавить("date");
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ТекстJSON);
	Возврат ПрочитатьJSON(ЧтениеJSON, ПрочитатьВСоответствие, ПоляДаты);
	
КонецФункции

&НаКлиенте
Функция ЗаписатьСтрокуJSON(Данные)
	
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет);
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
	ЗаписатьJSON(ЗаписьJSON, Данные);
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	VanessaTabs = ВладелецФормы.Элементы.VanessaEditor.Документ.defaultView.VanessaTabs;
	
	ТекстJSON = VanessaTabs.options;
	НаборОпций = ПрочитатьСтрокуJSON(ТекстJSON, Истина);
	Для каждого Элемент из НаборОпций Цикл
		СхемаРеквизита = Элемент.Получить("schema");
		Если СхемаРеквизита = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Опции.Добавить();
		НоваяСтрока.Код = Элемент.Получить("id");
		НоваяСтрока.Наименование = Элемент.Получить("name");
		НоваяСтрока.Описание = СхемаРеквизита.Получить("description");
		НоваяСтрока.Значение = ЗаписатьСтрокуJSON(Элемент.Получить("defaultValue"));
	КонецЦикла;

КонецПроцедуры
