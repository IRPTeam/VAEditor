Перем VanessaTabs;

&НаКлиенте
Функция ПрочитатьСтрокуJSON(ТекстJSON, ПрочитатьВСоответствие = Ложь)
	
	Если ПустаяСтрока(ТекстJSON) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПоляДаты = Новый Массив;
	ПоляДаты.Добавить("CreationDate");
	ПоляДаты.Добавить("date");
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.УстановитьСтроку(ТекстJSON);
	Возврат ПрочитатьJSON(ЧтениеJSON, ПрочитатьВСоответствие, ПоляДаты);
	
КонецФункции

&НаКлиенте
Функция ЗаписатьСтрокуJSON(Данные)
	
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет);
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
	ЗаписатьJSON(ЗаписьJSON, Данные);
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

&НаКлиенте
Процедура ДобавитьПодчиненные(Родитель, Данные, ТекЗначение, ПоУмолчанию)
	
	СхемаРеквизита = Данные.Получить("schema");
	СписокЗначений = Новый СписокЗначений;
	Для каждого Реквизит из СхемаРеквизита Цикл
		МассивИмен = СтрРазделить(Реквизит.Ключ, ".");
		Ключ = МассивИмен.Получить(МассивИмен.ВГраница());
		СписокЗначений.Добавить(Реквизит.Значение, Ключ);
	КонецЦикла;
	СписокЗначений.СортироватьПоПредставлению();
		
	Для каждого ЭлементСписка из СписокЗначений Цикл
		Ключ = ЭлементСписка.Представление;
		СхемаРеквизита = ЭлементСписка.Значение;
		НоваяСтрока = Родитель.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Наименование = Ключ;
		Попытка 
			НоваяСтрока.Значение = ЗаписатьСтрокуJSON(ТекЗначение.Получить(Ключ));
		Исключение
		КонецПопытки;
		Если СхемаРеквизита = Неопределено Тогда
			Продолжить;
		Иначе
			Попытка 
				НоваяСтрока.Тип = СхемаРеквизита.Получить("type");
				НоваяСтрока.Описание = СхемаРеквизита.Получить("description");
				НоваяСтрока.ПоУмолчанию = ЗаписатьСтрокуJSON(СхемаРеквизита.Получить("defaultValue"));
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	VanessaTabs = ВладелецФормы.Элементы.VanessaEditor.Документ.defaultView.VanessaTabs;
	
	ТекстJSON = VanessaTabs.options;
	НаборОпций = ПрочитатьСтрокуJSON(ТекстJSON, Истина);

	ТекстJSON = VanessaTabs.current.options;
	ТекущиеНастройки = ПрочитатьСтрокуJSON(ТекстJSON, Истина);
	
	Для каждого Опция из НаборОпций Цикл
		Ключ = Опция[0];
		Элемент = Опция[1];
		Если Ключ = "_never_" Тогда
			Продолжить;
		КонецЕсли;
		ТекЗначение = ТекущиеНастройки.Получить(Ключ);
		ПоУмолчанию = Элемент.Получить("defaultValue");
		НоваяСтрока = Опции.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Наименование = Ключ;
		НоваяСтрока.Значение = ЗаписатьСтрокуJSON(ТекЗначение);
		НоваяСтрока.ПоУмолчанию = ЗаписатьСтрокуJSON(ПоУмолчанию);
		СхемаРеквизита = Элемент.Получить("schema");
		Если СхемаРеквизита = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока.Схема = ЗаписатьСтрокуJSON(СхемаРеквизита);
		НоваяСтрока.Тип = СхемаРеквизита.Получить("type");
		НоваяСтрока.Описание = СхемаРеквизита.Получить("description");
		Если ПустаяСтрока(НоваяСтрока.Описание) Тогда
			НоваяСтрока.Описание = СхемаРеквизита.Получить("markdownDescription");
		КонецЕсли;
		Если ПустаяСтрока(НоваяСтрока.Тип) Тогда 
			НаборТипов = СхемаРеквизита.Получить("anyOf");
			Если НаборТипов = Неопределено Тогда
				ДобавитьПодчиненные(НоваяСтрока, Элемент, ТекЗначение, ПоУмолчанию)
			Иначе
				НоваяСтрока.Тип = ЗаписатьСтрокуJSON(НаборТипов);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОпцииПередСворачиванием(Элемент, Строка, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры
